apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def bintrayUser = properties.getProperty('bintray.user')
def bintrayApiKey = properties.getProperty('bintray.apikey')
def libraryVersion = rootProject.properties['library.version']
def libraryVersionDesc = rootProject.properties['library.version.desc']

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId 'com.manuege.boxfit'
            artifactId 'boxfit-processor'
            version libraryVersion
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayApiKey
    publications = ['MyPublication']
    pkg {
        repo = 'maven'
        name = 'boxfit-processor'
        userOrg = user
        licenses = ['MIT']
        vcsUrl = 'https://github.com/ManueGE/Boxfit.git'
        websiteUrl = 'https://github.com/ManueGE/Boxfit'
        issueTrackerUrl = 'https://github.com/ManueGE/Boxfit/issues'
        desc = 'Put together Retrofit and Objectbox.'
        labels = ['retrofit', 'objectbox', 'android']
        publicDownloadNumbers = true
        publish = true
        version {
            name = libraryVersion
            vcsTag = libraryVersion
            desc = libraryVersionDesc
        }
    }
}